plugins {
    id 'com.android.library'
    id 'kotlin-multiplatform'
    id 'io.gitlab.arturbosch.detekt'
    id 'org.jetbrains.dokka'
    id 'jacoco'
}

jacoco {
    toolVersion = jacoco_version
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 15
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

kotlin {
    android {
        publishLibraryVariantsGroupedByFlavor = true
    }
    targets {
        fromPreset(presets.android, 'android') {
            mavenPublication {
                artifactId = 'arbor-android'
            }
        }
    }

    sourceSets {
        androidMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib'
                api project(':common')
            }
        }
        androidTest {
            dependencies {
                implementation project(':common')
                implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
                implementation "org.mockito:mockito-core:3.1.0"
            }
        }
    }
}

task jacocoTestReportAndroid(type: JacocoReport) {
    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "$buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)

    classDirectories.from(files(debugTree))
    sourceDirectories.from(files("src/androidMain/kotlin"))
    executionData.from(fileTree(dir: "$buildDir/jacoco", include: "*.exec"))

    reports {
        xml.enabled = true
        html.enabled = true
    }
}
build.dependsOn jacocoTestReportAndroid

detekt {
    input = files("src/androidMain/kotlin")
    filters = ".*/resources/.*,.*/build/.*"
    config = project(':common').files("detekt.yml")
    reports.xml.enabled = false
}

task dokkaAndroid(type: org.jetbrains.dokka.gradle.DokkaTask) {
    impliedPlatforms = ["Android"]
    kotlinTasks { [] }
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc/android"
    sourceRoot {
        path = kotlin.sourceSets.androidMain.kotlin.srcDirs[0]
        platforms = ["Android"]
    }
}

task dokkaJavadocAndroidJar(type: Jar, dependsOn: dokkaAndroid) {
    classifier('javadoc')
    from "$buildDir/javadoc/android"
}

task androidSourcesJar(type: Jar) {
    classifier('sources')
    from kotlin.sourceSets.androidMain.kotlin.srcDirs
}

build.dependsOn("dokkaJavadocAndroidJar")
build.dependsOn("androidSourcesJar")

afterEvaluate {
    check.dependsOn('detekt')
}
