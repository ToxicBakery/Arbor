//file:noinspection GroovyAssignabilityCheck

buildscript {
    ext.kotlin_version = '2.1.0'
    ext.jacoco_version = '0.8.9'
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.6.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id("org.jetbrains.dokka") version "2.0.0"
    id "io.gitlab.arturbosch.detekt" version "1.23.7"
    //id 'nl.fabianm.kotlin.plugin.generated' version '1.5.0'
}

repositories {
    google()
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

def getGitCommitCount() {
    try {
        def process = "git rev-list HEAD --count".execute()
        return process.text.trim()
    } catch (Exception e) {
        logger.log(LogLevel.ERROR, "Failed to get Git commit count, is Git installed?", e)
        return null
    }
}

subprojects {
    final isCI = !"$System.env.CI".isEmpty()
    final isMaster = "$System.env.CIRCLE_BRANCH" == "master"
    final buildNumber = getGitCommitCount() ?: "0"

    group 'com.ToxicBakery.logging'
    version "2.0.$buildNumber" + (isCI && isMaster ? "" : "-SNAPSHOT")

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
}
